:alias X v2
:alias Y v3

:const HORIZ_MARGIN 8
:const OUTPUT_WIDTH 48
:const OUTPUT_HEIGHT 32
:calc HORIZ_END { HORIZ_MARGIN + OUTPUT_WIDTH }

# XOR the data at i directly to the screen
# Don't mess up vB - vE, make sure we update frame pointer
: plain-copy
  X := HORIZ_MARGIN
  Y := 0
  loop
    sprite X Y 1
    vF := 1
    i += vF
    vF := 1
    update-frame-pointer
    next-coordinate
    if Y != OUTPUT_HEIGHT then
  again
  return

# Do the RLE decoding
# i points to the first run length
# Don't mess up vB - vE, make sure we update frame pointer
: rle-decode
  X := HORIZ_MARGIN
  Y := 0
  loop
    load v0
    #if SCHIP
      vF := 1
      i += vF
    #end
    vF := 1
    update-frame-pointer
    v1 := 128
    v1 &= v0
    vF := 127
    v0 &= vF

    if v1 == 0 begin
      # This is a repeating run, sprite i to the screen v0 times
      loop
        sprite X Y 1
        next-coordinate
        v0 -= 1
        if v0 != 0 then
      again
      vF := 1
      i += vF
      vF := 1
      update-frame-pointer
    else
      # This is a copy run, sprite each byte to the screen
      vF := v0
      update-frame-pointer
      loop
        sprite X Y 1
        vF := 1
        i += vF
        next-coordinate
        v0 -= 1
        if v0 != 0 then
      again
    end

    if Y != OUTPUT_HEIGHT then
  again
  return

: next-coordinate
  X += 8
  if X == HORIZ_END begin
    X := HORIZ_MARGIN
    Y += 1
  end
  return
