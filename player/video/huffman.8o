:alias bitpointer v1
:alias temp v0

:alias value-hi v7
:alias value-lo v8
:alias code-hi v4
:alias code-lo v5

:alias numToGo v3
:alias bitsLength v9
:alias index vA
:alias bits v6

:alias mask-hi vB
:alias mask-lo vC
:alias addition-hi vD
:alias addition-lo vE

# Don't mess up v2 - v6 and vB - vE, make sure we update frame pointer
: huffman-decode
  bitpointer := 0 # Value 0 - 7 that tells us the bit offset
  loop
    # Free up a couple of registers
    i := safe-storage
    save v2 - v7
    i := safe-storage2
    save vB - vE

    # Load the bits to decode from the compressed data
    get-frame-pointer  # Point i to current data byte
    load v7 - v9
    # Shift the three loaded bytes so the first bit is the next value to decode
    temp := bitpointer
    loop
      while temp != 0
      temp -= 1
      v7 <<= v7
      v8 <<= v8
      v7 += vF
      v9 <<= v9
      v8 += vF
    again
    # v7 and v8 (value-hi and value-lo) now hold the next 16 bits to decode,
    # v0 and v9 are free again

    # Now we start the search for the right value given these 16 bits
    i := long huffman-codebook
    load bitsLength - bitsLength
    bits := 1
    index := 1
    code-hi := 0
    code-lo := 0
    mask-hi := 128
    mask-lo := 0
    addition-hi := 128
    addition-lo := 0
    loop
      i := long huffman-codebook
      i += bits
      load numToGo - numToGo
      loop
        while numToGo != 0
        numToGo -= 1
        # Compare first 'bits' bits of 'value' to 'code'
        temp := code-hi
        temp ^= value-hi
        temp &= mask-hi
        if temp == 0 begin
          temp := code-lo
          temp ^= value-lo
          temp &= mask-lo
          if temp == 0 then jump huffman-decode-found-value
        end
        index += 1
        code-hi += addition-hi
        code-lo += addition-lo
        code-hi += vF
      again
      # "code <<= 1", but done by adding a lower value to it
      addition-lo >>= addition-lo
      addition-hi >>= addition-hi
      if vF != 0 then addition-lo += 128

      # Fill up mask
      mask-lo += addition-lo
      mask-hi += addition-hi

      bits += 1
      if bits <= bitsLength then
    again

    # We should never get here. If we do, that's an error in the data
    loop again

: huffman-decode-found-value
    # Restore these registers so we can update the frame pointer
    i := safe-storage2
    load vB - vE
    # How many bits have we processed?
    bitpointer += bits
    # Have we processed so many bits that we can update the frame pointer?
    loop
      while bitpointer > 7
      bitpointer -= 8
      vF := 1
      update-frame-pointer
    again

    # Restore these registers so we can use and update the screen coordinates
    i := safe-storage
    load v2 - v7

    # Point i to the correct byte, show it on screen
    i := long huffman-codebook
    i += bitsLength
    i += index
    draw-byte

    # Update screen location and check if we're done
    next-coordinate
    if Y < height then
  again

  vF := 1
  update-frame-pointer
  :if SMOOTHING
    jump smoothing
  :else
    return
  :end

:if HUFFMAN_PRECOMPUTE
# Compute the first 128 entries in the Huffman codebook and save them to the
# memory below 0x200 ðŸ™ˆ

:alias pc-temp v0
:alias pc-bits v1
:alias pc-code-hi v2
:alias pc-code-lo v3

:alias pc-addition-hi v4
:alias pc-addition-lo v5

:alias pc-index v6
:alias pc-num v7
:alias pc-numToGo v8
:alias pc-bitsLength v9

: huffman-precompute-table
  i := long huffman-codebook
  load pc-bitsLength - pc-bitsLength
  i := huffman-precompute-values
  load v7
  # pc-bits := 1
  # pc-code-hi := 0
  # pc-code-lo := 0
  # pc-addition-hi := 128
  # pc-addition-lo := 0
  # pc-index := 1
  # pc-num := 0
  loop
    i := long huffman-codebook
    i += pc-bits
    load pc-numToGo - pc-numToGo
    loop
      while pc-numToGo != 0
      pc-numToGo -= 1
      # Store bits, code and corresponding value
      # Fetch value
      i := long huffman-codebook
      i += pc-bitsLength
      i += pc-index
      load pc-temp
      i := 0
      i += pc-num  i += pc-num
      i += pc-num  i += pc-num
      save v3
			pc-num += 1
      if pc-num == 128 then return
      pc-index += 1
      pc-code-hi += pc-addition-hi
      pc-code-hi += pc-addition-hi
      pc-code-lo += pc-addition-lo
      pc-code-hi += vF
    again
    # "code <<= 1", but done by adding a lower value to it
    pc-addition-lo >>= pc-addition-lo
    pc-addition-hi >>= pc-addition-hi
    if vF != 0 then pc-addition-lo += 128

    pc-bits += 1
    if pc-bits <= pc-bitsLength then
  again
  :breakpoint loop-done
  return

: huffman-precompute-values
  0 1 0 0 128 0 1 0
:end
